apply plugin: 'com.android.application'
apply plugin: 'realm-android'//依赖Realm数据库，插件化依赖
apply plugin: 'com.mob.sdk'
def releaseTime() {
    return new Date().format("yyyy.MM.dd", TimeZone.getTimeZone("UTC"))
}
android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.wuc.yuku"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true  //突破应用方法数65535的一个限制
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "color"
        ndk {
            //选择要添加的对应 cpu 类型的 .so 库。
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64', 'mips', 'mips64'
        }

        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "f7baeafa0c58a66d0681d590", //JPush 上注册的包名对应的 Appkey.
                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
        ]
    }

    //多渠道脚本支持
    productFlavors {
        kuan {}
        xiaomi {}
        qh360 {}
        baidu {}
        wandoujia {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    //签名
    signingConfigs {
        debug {
            storeFile file("video.jks")//签名文件
            storePassword "123456"
            keyAlias "video"
            keyPassword "123456"//签名密码
        }
        //签名打包
        release {
            storeFile file("video.jks")//签名文件
            storePassword "123456"
            keyAlias "video"
            keyPassword "123456"//签名密码
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.all {
                    // 输出apk名称为app_kuan_v1.0_2015.01.15.apk
                    outputFileName = "app_${variant.productFlavors[0].name}_v${variant.versionName}_${releaseTime()}.apk"
                }
            }
        }
    }
}

MobSDK {
    appKey "299e36f0713ac"
    appSecret "6a2856810393ffda5108dbd0dec3b807"

    ShareSDK {
        //平台配置信息
        Wechat {
            appId "wx4868b35061f87885"
            appSecret "64020361b8ec4c99936c0e3999a9f249"
        }
        QQ {
            appId "100371282"
            appKey "aed9b0303e3ed1e27bae87c33761161d"
            shareByAppClient true
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation "com.android.support:design:28.0.0"
    //依赖library原工程
    implementation project(':videosdk')
    //日志库logger
    implementation 'com.orhanobut:logger:2.2.0'

    //okttp依赖
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'com.alibaba:fastjson:1.2.54'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.youth.banner:banner:1.4.10'
    //添加 zxing
    implementation 'com.google.zxing:android-core:3.3.0'
    implementation 'com.google.zxing:core:3.3.3'
    //PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    implementation 'cn.jiguang.sdk:jpush:3.1.6'
    implementation 'cn.jiguang.sdk:jcore:1.2.5'
    implementation 'com.android.support:multidex:1.0.3'
    implementation  'com.umeng.umsdk:analytics:8.0.0'
    implementation  'com.umeng.umsdk:common:2.0.0'

}
